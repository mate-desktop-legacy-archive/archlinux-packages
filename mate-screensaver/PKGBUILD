# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-screensaver
pkgver=
pkgrel=1
pkgdesc='Screensaver for MATE'
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('dbus' 'desktop-file-utils' 'gtk2' 'libmatekbd' 'libnotify' 'libxss'
         'libxt' 'libxxf86vm' 'mate-desktop' 'mate-icon-theme'
         'mate-menus' 'mate-session-manager' 'pam' 'scrnsaverproto'
         'startup-notification' 'systemd')
makedepends=('mate-common' 'perl-xml-parser' 'xmlto' 'yelp-tools')
optdepends=('mate-power-manager: Add power management support.')
options=('!emptydirs' '!libtool')
groups=('mate')
source=("${pkgname}::git+https://github.com/mate-desktop/${pkgname}.git")        
sha1sums=('SKIP')
install=${pkgname}.install

pkgver() {
    cd ${srcdir}/${pkgname}
    echo 1.6.9.$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
    cd ${srcdir}/${pkgname}
    ./autogen.sh \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --with-xscreensaverdir=/usr/share/xscreensaver/config \
        --with-xscreensaverhackdir=/usr/lib/xscreensaver \
        --with-mit-ext \
        --with-libnotify \
        --enable-locking \
        --disable-static
    make
}

package() {
    cd ${srcdir}/${pkgname}
    make DESTDIR=${pkgdir} install
}
