# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-screensaver
pkgver=1.6.9.115.0460034
pkgrel=1
pkgdesc='Screensaver for MATE'
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('dbus-glib' 'libmatekbd' 'libnotify' 'libxss' 'mate-desktop'
         'mate-menus' 'mate-session-manager')
makedepends=('mate-common' 'perl-xml-parser' 'xmlto' 'yelp-tools')
optdepends=('mate-power-manager: Add power management support.')
options=('!emptydirs')
groups=('mate-extra')
source=("${pkgname}::git+https://github.com/mate-desktop/${pkgname}.git")
sha1sums=('SKIP')
install=${pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo 1.6.9.$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
    cd "${srcdir}/${pkgname}"
    ./autogen.sh \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --with-xscreensaverdir=/usr/share/xscreensaver/config \
        --with-xscreensaverhackdir=/usr/lib/xscreensaver \
        --with-mit-ext \
        --with-libnotify \
        --enable-locking \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
