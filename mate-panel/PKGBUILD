# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-panel
pkgver=1.8.0
pkgrel=2
pkgdesc="The MATE Panel"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('caja' 'dbus-glib' 'dconf' 'gtk2' 'libwnck' 'libcanberra' 'libice'
         'libmateweather' 'librsvg' 'libsm' 'libsoup' 'libxau' 'marco'
         'mate-menus' 'mate-desktop' 'mate-session-manager')
makedepends=('gobject-introspection' 'mate-common' 'networkmanager'
             'perl-xml-parser' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate')
source=("http://pub.mate-desktop.org/releases/1.8/${pkgname}-${pkgver}.tar.xz"
        "https://github.com/City-busz/mate-panel/commit/f4d754c48a1cb141407ca28d80431cbd954a5ef5.diff")
sha1sums=('289bca55fb26465f2c155d087932d6973edf07d9'
          '8c67a6aacfad3ee8225410f1ff1e37c6da3d5bc8')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -Np1 -i "${srcdir}/f4d754c48a1cb141407ca28d80431cbd954a5ef5.diff"
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --enable-introspection \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
