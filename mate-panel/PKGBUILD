# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.4
pkgbase=mate-panel
pkgname=(${pkgbase})
pkgver=0
pkgrel=1
pkgdesc="The MATE Panel (GTK2 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('caja>=1.9.3' 'dbus-glib' 'dconf' 'gtk2' 'libwnck' 'libcanberra' 'libice'
         'libmateweather>=1.9.1' 'librsvg' 'libsm' 'libsoup' 'libxau' 'marco>=1.9.3'
         'mate-menus>=1.9.1' 'mate-desktop>=1.9.3' 'mate-session-manager>=1.9.3')
makedepends=('git' 'gobject-introspection' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
source=("${pkgbase}"::"git+https://github.com/mate-desktop/${pkgbase}.git")
groups=('mate-unstable')
conflicts=("${pkgbase}-gtk3")
sha1sums=('SKIP')
install=${pkgbase}.install

pkgver() {
    cd "${srcdir}/${pkgbase}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgbase}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgbase} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --enable-introspection \
        --disable-static
    make
}

package_mate-panel() {
    cd "${srcdir}/${pkgbase}"
    make DESTDIR="${pkgdir}" install
}
