# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-panel
pkgver=1.8.1
pkgrel=2
pkgdesc="The MATE Panel"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('caja' 'dbus-glib' 'dconf' 'gtk2' 'libwnck' 'libcanberra' 'libice'
         'libmateweather' 'librsvg' 'libsm' 'libsoup' 'libxau' 'marco'
         'mate-menus' 'mate-desktop' 'mate-session-manager')
makedepends=('gobject-introspection' 'mate-common' 'networkmanager'
             'perl-xml-parser' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate')
source=("http://pub.mate-desktop.org/releases/1.8/${pkgname}-${pkgver}.tar.xz"
        "update_wm_name.diff")
sha1sums=('18795801763059269573015f33f16651a1fd36bc'
          '45c1228e51dbcec7c9e17e665115e81c81b89e32')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -Np1 -i "${srcdir}/update_wm_name.diff"
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --enable-introspection \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
