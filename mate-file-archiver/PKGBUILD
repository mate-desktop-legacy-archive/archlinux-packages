# Contributor: Alessio Sergi <asergi at archlinux dot us>
# Mantainer: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>

pkgname=mate-file-archiver
pkgver=1.6.0
pkgrel=1
pkgdesc="Archive manipulator for MATE"
arch=('i686' 'x86_64')
license=('GPL')
depends=('desktop-file-utils' 'mate-conf' 'hicolor-icon-theme')
makedepends=('mate-doc-utils' 'intltool')
conflicts=('file-roller' 'file-roller2' 'mate-extract')
optdepends=('unrar: for RAR uncompression'
'zip: for ZIP archives' 'unzip: for ZIP archives'
'p7zip: 7zip compression utility' 'arj: for ARJ archives'
'unace: extraction tool for the proprietary ace archive format')
options=('!libtool' '!emptydirs')
install=mate-file-archiver.install
url="http://mate-desktop.org"
groups=('mate-extras')
source=(http://pub.mate-desktop.org/releases/1.6/$pkgname-$pkgver.tar.xz)
sha256sums=('b959c41d1c86991e4f72224df0519bf0aaccfdd49afd8e799268b779ce9d2285')

build() {
	cd "$srcdir/$pkgname-$pkgver"
  ./autogen.sh --prefix=/usr \
		--disable-static \
		--localstatedir=/var \
		--libexecdir=/usr/lib/${pkgname} \
		--disable-scrollkeeper \
		--disable-schemas-install \
		--sysconfdir=/etc \
		--disable-packagekit
	mate-doc-prepare --force
	autoreconf -i
	aclocal

	make
}

package() {
    cd "${srcdir}/$pkgname-$pkgver"

    make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
