# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-netbook
pkgver=1.8.0
pkgrel=1
pkgdesc="A simple window management tool."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk2' 'libfakekey' 'libwnck' 'libunique' 'mate-panel')
makedepends=('mate-common' 'perl-xml-parser' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
options=('!emptydirs')
source=("http://pub.mate-desktop.org/releases/1.8/${pkgname}-${pkgver}.tar.xz")
sha1sums=('64723fcf6a3a42e7faacfb54fb0c85103af5b1ba')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexec=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --with-gtk=2.0 \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
