# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-netbook
pkgver=1.6.9.41.73b9047
pkgrel=1
pkgdesc="A simple window management tool."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk2' 'libfakekey' 'libwnck' 'libunique' 'mate-panel')
makedepends=('git' 'mate-common' 'perl-xml-parser' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
options=('!emptydirs')
source=("${pkgname}::git+https://github.com/mate-desktop/${pkgname}.git")
sha1sums=('SKIP')
install=${pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo 1.6.9.$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
    cd "${srcdir}/${pkgname}"
    ./autogen.sh \
        --prefix=/usr \
        --libexec=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --with-gtk=2.0 \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
