pkgname=mate-doc-utils
pkgver=1.2.1
pkgrel=1
pkgdesc="Documentation utilities for MATE"
arch=('any')
license=('GPL' 'LGPL')
depends=('libxslt' 'python2' 'docbook-xml' 'rarian' 'gnome-doc-utils')
makedepends=('pkgconfig' 'intltool' 'mate-common')
#conflicts=('gnome-doc-utils')
options=('!emptydirs' '!libtool')
url="http://matsusoft.com.ar/projects/mate/"
source=(http://pub.mate-desktop.org/releases/1.2/$pkgname-$pkgver.tar.xz)
sha256sums=('b537901a876db92805e4347590df2fb2ec5c913a69c12b52df5b20108a0beb10')

groups=('mate')

build() {
    cd "${srcdir}/$pkgname-$pkgver"
    PYTHON=/usr/bin/python2 ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --mandir=/usr/share/man \
        --localstatedir=/var \
        --disable-scrollkeeper || return 1
    make || return 1
}

package() {
    cd "${srcdir}/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install || return 1

    # files with conflicts with gnome-doc-utils
    rm -rf "${pkgdir}/usr/share/xml/mallard/"
    rm "${pkgdir}/usr/share/pkgconfig/xml2po.pc"
    rm -rf "${pkgdir}/usr/share/man/"
    rm -rf "${pkgdir}/usr/lib/"
    rm "${pkgdir}/usr/bin/xml2po"
}
