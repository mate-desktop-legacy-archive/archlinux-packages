# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Piotr Gorski <sir_lucjan@bridgelinux.pl>
# Contributor: Xpander <xpander0@gmail.com>

pkgname=caja
pkgver=1.7.0
pkgrel=3
pkgdesc="The MATE shell and file manager"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('dbus' 'desktop-file-utils' 'exempi' 'gtk2' 'gvfs' 'libexif' 'libunique'
         'libxt' 'mate-desktop' 'mate-icon-theme' 'startup-notification')
makedepends=('gobject-introspection' 'mate-common' 'pangox-compat')
optdepends=('gstreamer0.10-good-plugins: automatic media playback when mouseover')
replaces=('mate-file-manager')
provides=('mate-file-manager')
conflicts=('mate-file-manager')
options=('!emptydirs')
groups=('mate')
source=("http://pub.mate-desktop.org/releases/1.7/${pkgname}-${pkgver}.tar.xz")
sha1sums=('6549f3950cc50b54be90097f86ee4dc68f5bab06 ')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/caja \
        --enable-unique \
        --enable-introspection \
        --disable-static \
        --disable-update-mimedb
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
