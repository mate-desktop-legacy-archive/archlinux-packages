# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.3
pkgbase=caja
pkgname=(${pkgbase})
pkgver=0
pkgrel=1
pkgdesc="File manager for the MATE desktop (GTK2 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus' 'desktop-file-utils' 'exempi' 'gtk2' 'gvfs' 'libexif' 'libunique'
         'libxt' 'mate-desktop>=1.9.3' 'mate-icon-theme' 'pangox-compat'
         'startup-notification')
makedepends=('git' 'gobject-introspection' 'mate-common')
optdepends=('gstreamer0.10-good-plugins: automatic media playback when mouseover'
            'gvfs-smb: To connect to Samba/Windows shares')
groups=('mate-unstable')
provides=("${pkgbase}")
replaces=("${pkgbase}-gtk3")
conflicts=("${pkgbase}-gtk3")
source=("${pkgbase}"::"git+https://github.com/mate-desktop/${pkgbase}.git")
sha1sums=('SKIP')
install=${pkgbase}.install

pkgver() {
    cd "${srcdir}/${pkgbase}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgbase}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgbase} \
        --with-gtk=2.0 \
        --enable-unique \
        --enable-introspection \
        --disable-static \
        --disable-update-mimedb
    make
}

package_caja() {
    cd "${srcdir}/${pkgbase}"
    make DESTDIR="${pkgdir}" install
}
