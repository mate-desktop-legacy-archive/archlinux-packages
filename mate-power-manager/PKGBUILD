# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-power-manager
pkgver=1.6.9.167.751286f
pkgrel=1
pkgdesc="Session daemon that makes it easy to manage your laptop or desktop system for Mate Desktop"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('avahi' 'dbus' 'desktop-file-utils' 'gtk2' 'libcanberra'
         'libgcrypt' 'libgnome-keyring' 'libnotify' 'libunique'
         'mate-notification-daemon' 'mate-panel' 'upower' 'yelp')
makedepends=('docbook2x' 'docbook-xml' 'mate-common' 'perl-xml-parser' 'xmlto' 'yelp-tools')
options=('!emptydirs')
groups=('mate')
source=("${pkgname}::git+https://github.com/mate-desktop/${pkgname}.git")
sha1sums=('SKIP')
install=${pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo 1.6.9.$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
    cd "${srcdir}/${pkgname}"
    ./autogen.sh \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --enable-applets \
        --enable-unique
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
