#Mantainer: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>

pkgname=mate-menus
pkgver=1.4.0
pkgrel=1
pkgdesc="MATE menu specifications"
arch=('i686' 'x86_64')
license=('GPL')
depends=('glib2' 'python2')
makedepends=('intltool' 'gobject-introspection')
options=('!emptydirs' '!libtool')
source=(http://pub.mate-desktop.org/releases/1.4/$pkgname-$pkgver.tar.xz)
sha256sums=('c31c233bf2674646170277cc33962e24e74b98b8b575b7a33985f23c3feaaf92')

url="http://mate-desktop.org"
groups=('mate')

build() {
    cd "${srcdir}/$pkgname-$pkgver"
    PYTHON=/usr/bin/python2 ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var  \
        --enable-python \
        --disable-static || return 1
    make || return 1
}

package() {
    cd "${srcdir}/$pkgname-$pkgver"
    make DESTDIR="${pkgdir}" install || return 1
}
