# Contributor: Martin Wimpress <code@flexion.org>

pkgname=pluma
pkgver=1.7.0
pkgrel=4
pkgdesc="A powerful text editor for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('caja' 'desktop-file-utils' 'enchant' 'gtk2' 'gtksourceview2'
         'iso-codes' 'libsm' 'mate-desktop' 'python2' 'python2-gobject2'
         'pygtksourceview2' 'yelp')
makedepends=('mate-common' 'perl-xml-parser' 'yelp-tools')
options=('!emptydirs' '!libtool')
groups=('mate-extras')
source=(http://pub.mate-desktop.org/releases/1.7/${pkgname}-${pkgver}.tar.xz)
sha1sums=('49a0fec29728f5ffa36ab41415574d1e27f58536')
install=${pkgname}.install

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib \
        --enable-python        
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
}
