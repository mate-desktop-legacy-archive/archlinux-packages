# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mozo
pkgver=1.6.9.22.70fd5cb
pkgrel=1
pkgdesc="An powerfull text editor for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('desktop-file-utils' 'pygtk' 'python2' 'python2-gobject2')
makedepends=('mate-common' 'perl-xml-parser' 'yelp-tools')
replaces=('mate-menu-editor')
provides=('mate-menu-editor')
conflicts=('mate-menu-editor')
options=('!emptydirs')
groups=('mate-extras')
source=("${pkgname}::git+https://github.com/mate-desktop/${pkgname}.git")
sha1sums=('SKIP')
install=${pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo 1.6.9.$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
    cd "${srcdir}/${pkgname}"
    PYTHON=/usr/bin/python2 ./autogen.sh \
        --prefix=/usr
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
