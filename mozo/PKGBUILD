# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mozo
pkgver=1.6.9.22.70fd5cb
pkgrel=1
pkgdesc="MATE menu editing tool"
url="http://mate-desktop.org"
arch=('any')
license=('GPL')
depends=('mate-menus' 'pygtk' 'python2' 'python2-gobject2')
makedepends=('mate-common' 'perl-xml-parser' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
replaces=('mate-menu-editor')
provides=('mate-menu-editor')
conflicts=('mate-menu-editor')
options=('!emptydirs')
groups=('mate-extra')
source=("${pkgname}::git+https://github.com/mate-desktop/${pkgname}.git")
sha1sums=('SKIP')
install=${pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo 1.6.9.$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
    cd "${srcdir}/${pkgname}"
    PYTHON=/usr/bin/python2 ./autogen.sh \
        --prefix=/usr \
        --with-gtk=2.0
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
