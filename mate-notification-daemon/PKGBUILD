# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-notification-daemon
pkgver=1.6.9.71.9f4203a
pkgrel=1
pkgdesc="Notification daemon for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL' 'LGPL')
depends=('dconf' 'dbus-glib' 'gtk2' 'libcanberra' 'libwnck' 'libnotify')
makedepends=('mate-common' 'perl-xml-parser')
options=('!emptydirs')
groups=('mate')
provides=('notification-daemon')
source=("${pkgname}::git+https://github.com/mate-desktop/${pkgname}.git")
sha1sums=('SKIP')
install=${pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo 1.6.9.$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
    cd "${srcdir}/${pkgname}"
    ./autogen.sh \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
