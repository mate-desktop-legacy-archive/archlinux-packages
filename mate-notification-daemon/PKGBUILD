# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-notification-daemon
pkgver=1.7.0
pkgrel=1
pkgdesc="Notification daemon for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('dconf' 'dbus-glib' 'gtk2' 'libcanberra' 'libwnck' 'libnotify')
makedepends=('mate-common' 'perl-xml-parser')
options=('!emptydirs')
groups=('mate')
provides=('notification-daemon')
source=("${pkgname}::git+https://github.com/mate-desktop/${pkgname}.git")
sha1sums=('7640d4c78b1d0b6ed5afc28a9d9cce231fcbb816')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}"
    ./autogen.sh \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --with-gtk=2.0 \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
