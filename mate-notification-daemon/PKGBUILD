# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.10
pkgbase=mate-notification-daemon
pkgname=(${pkgbase} ${pkgbase}-gtk3)
pkgver=${_ver}.0
pkgrel=1
provides=('notification-daemon')
pkgdesc="Notification daemon for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('dconf' 'dbus-glib' 'gtk2' 'gtk3' 'libcanberra' 'libwnck' 'libwnck3' 'libnotify')
makedepends=('mate-common')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgbase}-${pkgver}.tar.xz")
sha1sums=('44407390f6216fcea5e02615d8e2b57b2502f358')
install=${pkgname}.install

prepare() {
    cd "${srcdir}"
    mv "${pkgname}-${pkgver}" "${pkgbase}-gtk2"
    cp -a "${pkgbase}-gtk2" "${pkgbase}-gtk3"
}

build() {
    cd "${srcdir}/${pkgbase}-gtk2"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgbase} \
        --with-gtk=2.0 \
        --disable-static
    make

    cd "${srcdir}/${pkgbase}-gtk3"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgbase} \
        --with-gtk=3.0 \
        --disable-static
    make
}

package_mate-notification-daemon() {
    pkgdesc+=' (GTK2 version)'
    groups=('mate')
    conflicts=("${pkgbase}-gtk3")
    depends=('dconf' 'dbus-glib' 'gtk2' 'libcanberra' 'libwnck' 'libnotify')

    cd "${srcdir}/${pkgbase}-gtk2"
    make DESTDIR="${pkgdir}" install
}

package_mate-notification-daemon-gtk3() {
    pkgdesc+=' (GTK3 version [EXPERIMENTAL])'
    groups=('mate-gtk3')
    conflicts=("${pkgbase}")
    depends=('dconf' 'dbus-glib' 'gtk3' 'libcanberra' 'libwnck3' 'libnotify')

    cd "${srcdir}/${pkgbase}-gtk3"
    make DESTDIR="${pkgdir}" install
}
