# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=caja-extensions
pkgver=1.7.1
pkgrel=1
pkgdesc="Caja extensions."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('caja' 'dbus-glib' 'gupnp' 'imagemagick' 'gtk2' 'samba')
makedepends=('mate-common' 'perl-xml-parser')
optdepends=('pidgin: Support for sending to instant messaging networks.'
            'gajim: Support for sending to Jabber.'
            'mate-bluetooth: Support for sending to bluetooth.')
conflicts=('mate-file-manager-gksu' 'mate-file-manager-image-converter' 'mate-file-manager-open-terminal' 'mate-file-manager-sendto' 'mate-file-manager-share')
replaces=('mate-file-manager-gksu' 'mate-file-manager-image-converter' 'mate-file-manager-open-terminal' 'mate-file-manager-sendto' 'mate-file-manager-share')
options=('!emptydirs')
groups=('mate-unstable-extra')
source=("http://pub.mate-desktop.org/releases/1.7/${pkgname}-${pkgver}.tar.xz")
sha1sums=('ffce502158bdacfda08c18e9448465cb48f340f3')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --with-gtk=2.0
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
