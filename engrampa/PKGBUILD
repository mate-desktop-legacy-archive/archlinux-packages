# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: hekel <hekel(at)archlinux.info>

pkgname=engrampa
pkgver=1.6.9.85.0e4df16
pkgrel=1
pkgdesc="Archive manipulator for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('bzip2' 'caja' 'desktop-file-utils' 'gtk2' 'gzip' 'libarchive' 'tar')
makedepends=('mate-common' 'perl-xml-parser' 'yelp-tools')
optdepends=('unrar: for RAR uncompression'
'zip: for ZIP archives' 'unzip: for ZIP archives'
'p7zip: 7zip compression utility' 'arj: for ARJ archives'
'unace: extraction tool for the proprietary ace archive format'
'yelp: for reading MATE help documents')
replaces=('mate-file-archiver')
provides=('mate-file-archiver')
conflicts=('mate-file-archiver')
options=( '!emptydirs')
groups=('mate-extra')
source=("${pkgname}::git+https://github.com/mate-desktop/${pkgname}.git"
        fr-rpm-bsdtar.patch)
sha1sums=('SKIP'
          '219b05a979bf6f249aaae27964f02345fd81168d')
install=${pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo 1.6.9.$(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
    cd "${srcdir}/${pkgname}"
    # This patch 'depends' on libarchive
    patch -Np1 -i "${srcdir}/fr-rpm-bsdtar.patch"
}

build() {
    cd "${srcdir}/${pkgname}"
    ./autogen.sh \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --disable-static \
        --disable-scrollkeeper \
        --disable-packagekit
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
