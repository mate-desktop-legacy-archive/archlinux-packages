# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=engrampa
pkgver=1.7.1
pkgrel=1
pkgdesc="Archive manipulator for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('bzip2' 'caja' 'desktop-file-utils' 'gtk2' 'gzip' 'libarchive' 'tar')
makedepends=('mate-common' 'perl-xml-parser' 'yelp-tools')
optdepends=('unrar: for RAR uncompression'
'zip: for ZIP archives' 'unzip: for ZIP archives'
'p7zip: 7zip compression utility' 'arj: for ARJ archives'
'unace: extraction tool for the proprietary ace archive format'
'yelp: for reading MATE help documents')
replaces=('mate-file-archiver')
provides=('mate-file-archiver')
conflicts=('mate-file-archiver')
options=( '!emptydirs')
groups=('mate-unstable-extra')
source=("http://pub.mate-desktop.org/releases/1.7/${pkgname}-${pkgver}.tar.xz"
        fr-rpm-bsdtar.patch)
sha1sums=('e5708566f587538273bfa30398f51e1279b2aed6'
          '219b05a979bf6f249aaae27964f02345fd81168d')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    # This patch 'depends' on libarchive
    patch -Np1 -i "${srcdir}/fr-rpm-bsdtar.patch"
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --with-gtk=2.0 \
        --disable-static \
        --disable-scrollkeeper \
        --disable-packagekit
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
