# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-utils
pkgver=1.6.9.101.a3b9d05
pkgrel=1
pkgdesc="Common MATE utilities for viewing disk usage, logs and fonts, taking screenshots, managing dictionaries and searching files"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('desktop-file-utils' 'gtk2' 'libcanberra' 'libgtop' 'libsm' 'zlib')
makedepends=('git' 'mate-common' 'mate-panel' 'perl-xml-parser' 'rarian' 'yelp-tools')
optdepends=('mate-panel: Look up words in dictionaries from the panel'
            'yelp: for reading MATE help documents')
options=('!emptydirs')
groups=('mate-extra')
source=("${pkgname}::git+https://github.com/mate-desktop/${pkgname}.git")
sha1sums=('SKIP')
install=${pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo 1.6.9.$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
    cd "${srcdir}/${pkgname}"
    ./autogen.sh \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --with-gtk=2.0 \
        --sysconfdir=/etc \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
