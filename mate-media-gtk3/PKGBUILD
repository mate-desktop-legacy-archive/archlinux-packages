# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.2
pkgbase=mate-media
pkgname=(${pkgbase}-gtk3)
pkgver=0
pkgrel=1
url="http://mate-desktop.org"
pkgdesc="MATE Media Tools (GTK3 version [EXPERIMENTAL])"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'libcanberra-pulse' 'libmatemixer' 'libunique3' 'marco-gtk3>=1.9.3'
         'mate-desktop-gtk3>=1.9.3')
makedepends=('git' 'mate-common' 'mate-panel-gtk3>=1.9.4' 'mate-settings-daemon-gtk3>=1.9.5')
source=("${pkgbase}"::"git+https://github.com/mate-desktop/${pkgbase}.git")
groups=('mate-unstable-extra-gtk3')
provides=("${pkgbase}")
replaces=("${pkgbase}")
conflicts=("${pkgbase}")
sha1sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgbase}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgbase}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgbase} \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --disable-static
    make
}

package_mate-media-gtk3() {
    cd "${srcdir}/${pkgbase}"
    make DESTDIR="${pkgdir}" install
}
