# $Id: PKGBUILD 107986 2014-03-20 12:53:43Z flexiondotorg $
# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <barthalion@gmail.com>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgbase=galculator
pkgname=(galculator galculator-gtk2)
pkgver=2.1.3
pkgrel=4
pkgdesc='GTK+ based scientific calculator'
arch=('i686' 'x86_64')
url='http://galculator.sourceforge.net/'
license=('GPL')
depends=('gtk2' 'gtk3')
makedepends=('intltool')
install=${pkgbase}.install
source=("http://downloads.sourceforge.net/${pkgbase}/${pkgbase}-${pkgver}.tar.bz2")
sha256sums=('91ac62d6d74da910cb79c68f7bf3f4505ac0efe51091150c4ea54fe1c1306235')

build() {
    # GTK3 version [EXPERIMENTAL]
    cd "${srcdir}/${pkgbase}-${pkgver}"
    [ -d gtk3 ] || mkdir gtk3
    cd gtk3
    ../configure \
      --prefix=/usr
    make

    # GTK2 version
    cd "${srcdir}/${pkgbase}-${pkgver}"
    [ -d gtk2 ] || mkdir gtk2
    cd gtk2
    ../configure \
      --prefix=/usr \
      --disable-gtk3
    make
}

package_galculator() {
    pkgdesc+=' (GTK3 version)'
    depends=('gtk3')
    groups=('mate-extra-gtk3')
    replaces=('mate-calc')
    provides=('mate-calc')
    conflicts=('galculator-gtk3')

    cd "${pkgbase}-${pkgver}/gtk3"
    make DESTDIR="${pkgdir}" install
}

package_galculator-gtk2() {
    pkgdesc+=' (GTK2 version)'
    depends=('gtk2')
    groups=('mate-extra')
    replaces=('mate-calc')
    provides=('mate-calc')
    conflicts=('galculator')

    cd "${pkgbase}-${pkgver}/gtk2"
    make DESTDIR="${pkgdir}" install
}
