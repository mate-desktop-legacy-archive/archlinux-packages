# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.10
_pkgbase=mate-panel
pkgname=${_pkgbase}-gtk3
pkgver=${_ver}.0
pkgrel=2
pkgdesc="The MATE Panel (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('caja-gtk3>=1.10' 'dbus-glib' 'dconf' 'gtk3' 'libwnck3' 'libcanberra' 'libice'
         'libmateweather-gtk3>=1.10' 'librsvg' 'libsm' 'libsoup' 'libxau' 'marco-gtk3>=1.10'
         'mate-menus>=1.10' 'mate-desktop-gtk3>=1.10' 'mate-session-manager-gtk3>=1.10')
makedepends=('gobject-introspection' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
groups=('mate-gtk3')
conflicts=("${_pkgbase}")
sha1sums=('632d7127fba8c5fa76f5dd951db89325c6ca3f89')
install=${_pkgbase}.install

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${_pkgbase} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --enable-introspection \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
