# Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-document-viewer
pkgver=1.6.1
pkgrel=2
pkgdesc="Simply a document viewer"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('dbus' 'djvulibre' 'glib2' 'gobject-introspection' 'gtk2' 'libgxps'
         'libmatekeyring' 'libsm' 'libspectre' 'libtiff' 'libx11' 'libxml2'
         'mate-file-manager' 'mate-icon-theme' 'poppler-glib' 'shared-mime-info'
         't1lib' 'texlive-bin' 'zlib')
makedepends=('gtk-update-icon-cache' 'mate-common' 'mate-doc-utils' 'perl-xml-parser')
groups=('mate-extras')
install=mate-document-viewer.install
options=('!libtool' '!emptydirs')
source=(http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz)
sha256sums=('100feb278bb919de874e76bd2344cb49fa60e2819e98b1394226086e34bf59ce')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    # Work around a build system problem
    autoreconf -v -i --force
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --enable-gtk-doc \
        --enable-djvu \
        --enable-dvi \
        --enable-t1lib \
        --enable-pixbuf \
        --enable-comics \
        --disable-introspection \
        --disable-static \
        --disable-scrollkeeper
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    ln -s atril ${pkgdir}/usr/bin/mate-document-viewer
}
