# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.0
pkgbase=mate-control-center
pkgname=(${pkgbase}-gtk3)
pkgver=0
pkgrel=1
pkgdesc="The Control Center for MATE (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('caja-gtk3>=1.9.3' 'dbus-glib' 'dconf' 'desktop-file-utils' 'gsettings-desktop-schemas'
         'gtk3' 'mate-desktop-gtk3>=1.9.3' 'mate-icon-theme' 'mate-menus>=1.9.1' 'mate-settings-daemon-gtk3>=1.9.5'
         'marco-gtk3>=1.9.3' 'libgtop' 'libmatekbd-gtk3>=1.9.1' 'libunique3' 'librsvg' 'libxss'
         'libxt' 'startup-notification')
makedepends=('git' 'docbook2x' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-unstable-gtk3')
conflicts=("${pkgbase}")
source=("${pkgbase}"::"git+https://github.com/mate-desktop/${pkgbase}.git")
sha1sums=('SKIP')
install=${pkgbase}.install

pkgver() {
    cd "${srcdir}/${pkgbase}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgbase}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --disable-static \
        --disable-update-mimedb
    make
}

package_mate-control-center-gtk3() {
    cd "${srcdir}/${pkgbase}"
    make DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/share/applications/mimeinfo.cache"
}
