# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.10
_gtkver=3.16
pkgname=mate-themes
pkgver=${_ver}.1
pkgrel=1
pkgdesc="Default themes for the MATE desktop"
url="http://mate-desktop.org"
arch=('any')
license=('GPL')
depends=('gtk3' 'gtk-engines' 'gtk-engine-murrine' 'librsvg' 'mate-icon-theme')
makedepends=('mate-common' 'icon-naming-utils')
optdepends=('gtk-engine-unico: Support for some GTK3 themes.')
groups=('mate' 'mate-gtk3')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgname}-gtk${_gtkver}-${pkgver}.tar.xz")
sha1sums=('5260bc5841ac7a47695902de3741853eaf71f313')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-gtk${_gtkver}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --enable-all-themes \
        --enable-test-themes \
        --disable-placeholders
    make
}

package() {
    cd "${srcdir}/${pkgname}-gtk${_gtkver}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
