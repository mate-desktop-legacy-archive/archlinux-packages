# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mucharmap
pkgver=
pkgrel=1
pkgdesc="MATE Unicode Charmap"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('gtk2' 'pygtk' 'python2')
makedepends=('gobject-introspection' 'mate-common' 'perl-xml-parser' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
options=('!emptydirs')
groups=('mate-extra')
source=("${pkgname}::git+https://github.com/mate-desktop/${pkgname}.git")
sha1sums=('SKIP')
install=${pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo 1.6.9.$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
    cd "${srcdir}/${pkgname}"
    PYTHON=/usr/bin/python2 ./autogen.sh \
        --prefix=/usr \
        --enable-charmap \
        --enable-python-bindings \
        --enable-introspection \
        --disable-static \
        --disable-scrollkeeper
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
