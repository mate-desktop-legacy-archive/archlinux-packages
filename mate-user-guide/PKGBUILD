# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9
_pkgbase=mate-user-guide
pkgname=(${_pkgbase})
pkgver=${_ver}.90
pkgrel=1
pkgdesc="MATE User Guide"
groups=('mate' 'mate-gtk3')
url="http://mate-desktop.org"
arch=('any')
license=('GPL' 'LGPL')
depends=('yelp')
makedepends=('git' 'mate-common' 'yelp-tools')
source=("${_pkgbase}"::"git+https://github.com/mate-desktop/${_pkgbase}.git")
sha1sums=('SKIP')
install=${_pkgbase}.install

prepare() {
    cd "${srcdir}/${_pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}"

    cd "${srcdir}/${_pkgbase}"
    ./configure \
        --prefix=/usr \
        --enable-gtk-doc
    make
}

package() {
    cd "${srcdir}/${_pkgbase}"
    make DESTDIR="${pkgdir}" install
}
