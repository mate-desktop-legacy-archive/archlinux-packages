# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.2
pkgbase=mate-media
pkgname=(${pkgbase})
pkgver=0
pkgrel=1
url="http://mate-desktop.org"
pkgdesc="MATE Media Tools  (GTK2 version)"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk2' 'libcanberra-pulse' 'libmatemixer' 'libunique' 'marco>=1.9.3' 'mate-desktop>=1.9.3')
makedepends=('git' 'mate-common' 'mate-panel>=1.9.4' 'mate-settings-daemon>=1.9.5')
source=("${pkgbase}"::"git+https://github.com/mate-desktop/${pkgbase}.git")
groups=('mate-unstable-extra')
provides=("${pkgbase}")
replaces=("${pkgbase}-gtk3")
conflicts=("${pkgbase}-gtk3")
sha1sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgbase}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgbase}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgbase} \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --disable-static
    make
}

package_mate-media() {
    cd "${srcdir}/${pkgbase}"
    make DESTDIR="${pkgdir}" install
}
