# Maintainer : Martin Wimpress <code@flexion.org>

pkgbase=mate-media
pkgname=("${pkgbase}-pulseaudio" "${pkgbase}-gstreamer")
pkgver=1.6.9.85.68915ba
pkgrel=1
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gstreamer0.10-base-plugins' 'gtk2' 'libcanberra-pulse' 'libunique'
         'marco' 'mate-desktop' 'pulseaudio')
makedepends=('mate-common' 'mate-doc-utils' 'mate-panel'
             'mate-settings-daemon-gstreamer' 'perl-xml-parser' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
options=('!emptydirs')
source=("${pkgbase}::git+https://github.com/mate-desktop/${pkgbase}.git")
sha1sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgbase}"
    echo 1.6.9.$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
    cd "${srcdir}"
    cp -a ${pkgbase}{,-gstreamer}

    # build for pulseaudio
    cd "${srcdir}/${pkgbase}"
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgbase} \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --enable-pulseaudio \
        --disable-gstmix \
        --disable-gst-mixer-applet \
        --disable-static
    make

    # build for gstreamer
    cd "${srcdir}/${pkgbase}-gstreamer"
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgbase} \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --enable-gstmix \
        --enable-gst-mixer-applet \
        --enable-deprecated \
        --disable-pulseaudio \
        --disable-static
    make
}

package_mate-media-pulseaudio() {
    groups=('mate-extra')
    pkgdesc="MATE Media Tools (pulseaudio)"
    depends=('libcanberra-pulse' 'libunique' 'marco' 'mate-desktop' 'pulseaudio')
    conflicts=("${pkgbase}-gstreamer" 'mate-settings-daemon-gstreamer')
    provides=("${pkgbase}")
    replaces=("${pkgbase}")
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}

package_mate-media-gstreamer() {
    pkgdesc="MATE Media Tools (GStreamer)"
    depends=('gstreamer0.10-base-plugins' 'gtk2' 'libcanberra' 'marco' 
             'mate-desktop')
    optdepends=('mate-panel: Volume control for the panel')
    conflicts=("${pkgbase}-pulseaudio" 'mate-settings-daemon-pulseaudio')
    provides=("${pkgbase}")    
    install=${pkgbase}-gstreamer.install
    cd "${srcdir}/${pkgbase}-${pkgver}-gstreamer"
    make DESTDIR="${pkgdir}" install
}
