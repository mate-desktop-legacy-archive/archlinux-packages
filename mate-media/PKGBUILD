# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.10
_pkgbase=mate-media
pkgname=(${_pkgbase})
pkgver=${_ver}.0
pkgrel=1
url="http://mate-desktop.org"
pkgdesc="MATE Media Tools  (GTK2 version)"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk2' 'libcanberra-pulse' 'libmatemixer' 'libunique' 'marco>=1.10' 'mate-desktop>=1.10')
makedepends=('mate-common' 'mate-panel>=1.10' 'mate-settings-daemon>=1.10')
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
groups=('mate-extra')
replaces=("${_pkgbase}-gstreamer" "${_pkgbase}-pulseaudio")
conflicts=("${_pkgbase}-gtk3" "${_pkgbase}-gstreamer" "${_pkgbase}-pulseaudio")
sha1sums=('702b6aa48312eb58891b6cca887bd112e7f0b86c')

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${_pkgbase} \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
