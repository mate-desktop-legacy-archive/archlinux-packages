# Maintainer : Martin Wimpress <code@flexion.org>

pkgbase=mate-media
pkgname=("${pkgbase}-pulseaudio" "${pkgbase}-gstreamer")
pkgver=1.7.0
pkgrel=1
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gstreamer0.10-base-plugins' 'gtk2' 'libcanberra-pulse' 'libunique'
         'marco' 'mate-desktop' 'pulseaudio')
makedepends=('mate-common' 'mate-doc-utils' 'mate-panel'
             'mate-settings-daemon-gstreamer' 'perl-xml-parser' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
options=('!emptydirs')
source=("http://pub.mate-desktop.org/releases/1.7/${pkgbase}-${pkgver}.tar.xz")
sha1sums=('48e9b601c2170e9712a2407b900ed5cca9b37325')

build() {
    cd "${srcdir}"
    cp -a ${pkgbase}-${pkgver}{,-gstreamer}

    # build for pulseaudio
    cd "${srcdir}/${pkgbase}-${pkgver}"
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgbase} \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --enable-pulseaudio \
        --disable-gstmix \
        --disable-gst-mixer-applet \
        --disable-static
    make

    # build for gstreamer
    cd "${srcdir}/${pkgbase}-${pkgver}-gstreamer"
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgbase} \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --enable-gstmix \
        --enable-gst-mixer-applet \
        --enable-deprecated \
        --disable-pulseaudio \
        --disable-static
    make
}

package_mate-media-pulseaudio() {
    groups=('mate-unstable-extra')
    pkgdesc="MATE Media Tools (pulseaudio)"
    depends=('libcanberra-pulse' 'libunique' 'marco' 'mate-desktop' 'pulseaudio')
    conflicts=("${pkgbase}-gstreamer" 'mate-settings-daemon-gstreamer')
    provides=("${pkgbase}")
    replaces=("${pkgbase}")
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}

package_mate-media-gstreamer() {
    pkgdesc="MATE Media Tools (GStreamer)"
    depends=('gstreamer0.10-base-plugins' 'gtk2' 'libcanberra' 'marco' 
             'mate-desktop')
    optdepends=('mate-panel: Volume control for the panel')
    conflicts=("${pkgbase}-pulseaudio" 'mate-settings-daemon-pulseaudio')
    provides=("${pkgbase}")    
    install=${pkgbase}-gstreamer.install
    cd "${srcdir}/${pkgbase}-${pkgver}-gstreamer"
    make DESTDIR="${pkgdir}" install
}
