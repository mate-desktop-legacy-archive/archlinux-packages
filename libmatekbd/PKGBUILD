# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Piotr Gorski <sir_lucjan@bridgelinux.pl>
# Contributor: Xpander <xpander0@gmail.com>

pkgname=libmatekbd
pkgver=1.7.1
pkgrel=2
pkgdesc="MATE keyboard library"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('LGPL')
depends=('dconf' 'gtk2' 'iso-codes' 'libxklavier')
makedepends=('mate-common' 'perl-xml-parser')
options=('!emptydirs')
groups=('mate')
source=("http://pub.mate-desktop.org/releases/1.7/${pkgname}-${pkgver}.tar.xz")
sha1sums=('f1611ba217dd755af26ce1f9ab6cfb58efb8f525')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
