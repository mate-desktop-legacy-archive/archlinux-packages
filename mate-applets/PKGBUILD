# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-applets
pkgver=1.6.9.167.9002923
pkgrel=2
pkgdesc="Applets for MATE panel"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://mate-desktop.org"
license=('GPL')
depends=('avahi' 'dbus' 'gsettings-desktop-schemas'
         'gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins'
         'gtk2' 'gucharmap' 'gvfs' 'libgtop' 'libmateweather' 'libwnck'
         'libnotify' 'liboobs' 'libsoup' 'libxklavier' 'libxt'
         'mate-desktop' 'mate-icon-theme' 'mate-panel'
         'mate-settings-daemon' 'polkit' 'pygtk' 'python2' 'python2-dbus'
         'python2-gobject2' 'python2-notify' 'sharutils'
         'system-tools-backends' 'upower' 'yelp')
makedepends=('docbook2x' 'docbook-xsl' 'mate-common' 'networkmanager' 'perl-xml-parser' 'yelp-tools')
options=('!emptydirs')
groups=('mate-extras')
source=("${pkgname}::git+https://github.com/mate-desktop/${pkgname}.git")        
sha1sums=('SKIP')
install=${pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo 1.6.9.$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
    cd "${srcdir}/${pkgname}"
    PYTHON=/usr/bin/python2 ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgname} \
        --enable-polkit \
        --enable-networkmanager \
        --enable-ipv6 \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
    sed -i 's/env python/env python2/' "${pkgdir}/usr/lib/${pkgname}/invest-applet"
}
