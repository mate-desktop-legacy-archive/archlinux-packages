#Mantainer: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>

pkgname=mate-applets
pkgver=1.2.0
pkgrel=1
pkgdesc="Applets for MATE panel"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gstreamer0.10-base-plugins' 'mate-panel' 'libgtop' 'libmatenotify'
         'cpufrequtils')
makedepends=('mate-doc-utils' 'pkgconfig' 'mate-settings-daemon' 'intltool'
              'mate-icon-theme' 'networkmanager') # 'mate-python-desktop' 'mate-character-map'
groups=('mate-extras')
options=(!emptydirs)
replace=('gnome-applets')
conflicts=('gnome-applets')
url="http://mate-desktop.org"
install=mate-applets.install
source=(http://pub.mate-desktop.org/releases/1.2/$pkgname-$pkgver.tar.xz)
sha256sums=('23dd41dd616f3871ea75c93c2484ae7f89a00ee67960c77c8ceaa5e58462f81f')

build() {
    cd "${srcdir}/$pkgname-$pkgver"
    PYTHON=/usr/bin/python2 ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/mate-applets \
        --disable-static \
        --disable-scrollkeeper \
        --enable-polkit \
        --enable-networkmanager \
        --enable-mixer-applet \
        --enable-mini-commander \
        --enable-frequency-selector \
        --enable-ipv6 \
        --disable-schemas-install || return 1
	make || return 1
}

package() {
    cd "${srcdir}/$pkgname-$pkgver"
    make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

    install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
    mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
    rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
