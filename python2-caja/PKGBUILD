# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=python2-caja
_pkgname=python-caja
pkgver=1.7.90
pkgrel=1
pkgdesc="Python binding for Caja, to allow Caja property page and menu item extensions to be written in Python."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('caja' 'python2' 'python2-gobject')
makedepends=('mate-common')
options=('!emptydirs')
replaces=('python-caja')
provides=('python-caja')
source=("http://pub.mate-desktop.org/releases/1.7/${_pkgname}-${pkgver}.tar.xz")
sha1sums=('8a56b25c8835aefcd30e4e13043f3fa25914a323')

prepare() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    sed -i 's/python-config/python2-config/' configure
}

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
