# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=python2-caja
_pkgname=python-caja
pkgver=
pkgrel=1
pkgdesc="Python binding for Caja, to allow Caja property page and menu item extensions to be written in Python."
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('caja' 'pygtk' 'python2' 'python2-gobject')
makedepends=('mate-common')
options=('!emptydirs' '!libtool')
groups=('mate-extras')
replaces=('python-caja')
provides=('python-caja')
source=("${pkgname}::git+https://github.com/mate-desktop/${_pkgname}.git")
sha1sums=('SKIP')

pkgver() {
    cd ${srcdir}/${pkgname}
    echo 1.6.9.$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
    cd ${srcdir}/${pkgname}
    PYTHON=/usr/bin/python2 ./autogen.sh \
        --prefix=/usr \
        --disable-static
    make
}

package() {
    cd ${srcdir}/${pkgname}
    make DESTDIR=${pkgdir} install
}
