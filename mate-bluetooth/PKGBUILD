# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-bluetooth
pkgver=1.6.9.63.54e5688
pkgrel=1
pkgdesc="A collection of Bluetooth utilities and modules for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('dbus' 'desktop-file-utils' 'gobject-introspection' 'gtk2'
         'gvfs' 'libnotify' 'libunique' 'obex-data-server')
makedepends=('mate-common' 'caja-extensions' 'perl-xml-parser' 'yelp-tools')
optdepends=('mate-user-share: Enable sharing via bluetooth and other protocols.'
            'yelp: for reading MATE help documents')
options=('!emptydirs')
groups=('mate-extra')
source=("${pkgname}::git+https://github.com/mate-desktop/${pkgname}.git")
sha1sums=('SKIP')
install=${pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo 1.6.9.$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
    cd "${srcdir}/${pkgname}"
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-desktop-update \
        --disable-icon-update \
        --disable-introspection
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
