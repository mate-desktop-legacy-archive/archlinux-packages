#Mantainer: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>

pkgname=mate-bluetooth
pkgver=1.2.1
pkgrel=1
pkgdesc="A collection of Bluetooth utilities and modules for MATE"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libunique' 'libmatenotify' 'hicolor-icon-theme' 'gvfs-obexftp' 'obexd-client')
makedepends=('intltool' 'pygtk' 'mate-doc-utils' 'mate-file-manager-sendto' 'gobject-introspection')
options=('!emptydirs')
install=mate-bluetooth.install
url="http://mate-desktop.org"
groups=('mate-extras')
source=(http://pub.mate-desktop.org/releases/1.2/$pkgname-$pkgver.tar.xz)
sha256sums=('8f57225e82eefcb4825163f4166e8750ab3f07ebcdf6831bd6c8f9c2b35937fa')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./autogen.sh --prefix=/usr --sysconfdir=/etc \
		--localstatedir=/var --disable-desktop-update \
		--disable-icon-update || return 1
		#--disable-schemas-compile
	make || return 1
}

package() {
    cd "$srcdir/$pkgname-$pkgver"

	make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

	install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
	mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
	rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
