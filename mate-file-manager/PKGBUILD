#Mantainer: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>

pkgname=mate-file-manager
pkgver=1.4.0
pkgrel=1
pkgdesc="The MATE shell and file manager"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libexif' 'exempi' 'mate-icon-theme' 'shared-mime-info'
         'desktop-file-utils' 'gvfs' 'libunique' 'mate-desktop')
makedepends=('pkgconfig' 'intltool')
url="http://mate-desktop.org"
options=('!libtool' '!emptydirs')
install=mate-file-manager.install
source=(http://pub.mate-desktop.org/releases/1.4/$pkgname-$pkgver.tar.xz)
sha256sums=('cc70fbc72614f068fd8dd2aa8d3d9fe68d3f45da17671e20b1dfe8f70c6a2132')

groups=('mate')

build() {
    cd "${srcdir}/$pkgname-$pkgver"

    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --libexecdir=/usr/lib/caja \
        --enable-unique \
        --disable-update-mimedb || return 1

    make || return 1
}

package() {
    cd "${srcdir}/$pkgname-$pkgver"

    make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

    install -m755 -d ${pkgdir}/usr/share/mateconf/schemas

    mateconf-merge-schema ${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
    rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
