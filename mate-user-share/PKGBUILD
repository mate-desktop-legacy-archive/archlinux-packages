#Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-user-share
pkgver=
pkgrel=1
pkgdesc="User level public file sharing via WebDAV or ObexFTP."
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('apache' 'caja' 'dbus' 'desktop-file-utils' 'gtk2' 'libcanberra'
         'libnotify' 'libunique' 'mate-bluetooth' 'mod_dnssd'
         'obex-data-server')
makedepends=('mate-common' 'perl-xml-parser' 'yelp-tools')
options=('!emptydirs' '!libtool')
groups=('mate-extras')
source=("${pkgname}::git+https://github.com/mate-desktop/${pkgname}.git")        
sha1sums=('SKIP')         
install=${pkgname}.install

pkgver() {
    cd ${srcdir}/${pkgname}
    echo 1.6.9.$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
    cd ${srcdir}/${pkgname}
    PYTHON=/usr/bin/python2 ./autogen.sh \
        --prefix=/usr \
        --libexec=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --disable-static \
        --disable-scrollkeeper
    make
}

package() {
    cd ${srcdir}/${pkgname}
    make  DESTDIR=${pkgdir} install
    rm -f ${pkgdir}/usr/share/mate-user-share/dav_user_2.0.conf
}
