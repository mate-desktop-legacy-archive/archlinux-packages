#Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-user-share
pkgver=1.7.0
pkgrel=1
pkgdesc="User level public file sharing via WebDAV or ObexFTP."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('apache' 'caja' 'dbus-glib' 'dconf' 'libunique' 'mod_dnssd')
         #'mate-bluetooth' 'obex-data-server')
makedepends=('libcanberra' 'libnotify' 'mate-common' 'perl-xml-parser' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
options=('!emptydirs')
source=("http://pub.mate-desktop.org/releases/1.7/${pkgname}-${pkgver}.tar.xz")
sha1sums=('a8424c7cdcd35a0007241fb1c8a3e1b25baea1dc')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --libexec=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --disable-static \
        --disable-scrollkeeper \
        --disable-bluetooth
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make  DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/share/mate-user-share/dav_user_2.0.conf"
}
