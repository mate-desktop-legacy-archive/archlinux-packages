# Maintainer : Martin Wimpress <code@flexion.org>

_pkgname=mate-media
pkgname=${_pkgname}-gstreamer
pkgver=1.6.9.85.68915ba
pkgrel=1
pkgdesc="MATE Media Tools (GStreamer)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('gstreamer0.10-base-plugins' 'gtk2' 'libcanberra' 'marco'
         'mate-desktop')
makedepends=('mate-common' 'mate-panel' 'mate-settings-daemon-gstreamer'
             'perl-xml-parser' 'yelp-tools')
optdepends=('mate-panel: Volume control for the panel'
            'yelp: for reading MATE help documents')
options=('!emptydirs')
conflicts=("${_pkgname}-pulseaudio" 'mate-settings-daemon-pulseaudio')
provides=("${_pkgname}")
source=("${_pkgname}::git+https://github.com/mate-desktop/${_pkgname}.git"
        enable_deprecated.diff)
sha1sums=('SKIP'
          'f0e38578aacacf9ee852b7a44a34908cc230b91f')
install=${pkgname}.install

pkgver() {
    cd "${srcdir}/${_pkgname}"
    echo 1.6.9.$(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
    cd "${srcdir}/${_pkgname}"
    patch -p1 < ../../enable_deprecated.diff gst-mixer/src/Makefile.am
}

build() {
    cd "${srcdir}/${_pkgname}"
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${_pkgname} \
        --localstatedir=/var \
        --enable-gstmix \
        --enable-gst-mixer-applet \
        --disable-pulseaudio \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${_pkgname}"
    make DESTDIR="${pkgdir}" install
}
