# Maintainer : Martin Wimpress <code@flexion.org>

pkgbase=mate-settings-daemon
pkgname=("${pkgbase}-pulseaudio" "${pkgbase}-gstreamer")
pkgver=1.8.2
pkgrel=3
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'dconf' 'fontconfig' 'gstreamer0.10-base' 'gtk2'
         'libcanberra-pulse' 'libmatekbd' 'libnotify' 'libxt' 'mate-desktop'
         'nss' 'polkit' 'pulseaudio-alsa')
makedepends=('mate-common')
source=("http://pub.mate-desktop.org/releases/1.8/${pkgbase}-${pkgver}.tar.xz"
        'use_galculator_first.diff'
        'add_a11y_settings_plugin.diff'
        'move_a11y_keybindings_to_media-keys_plugin.diff'
        'use_gnome_a11y_schema_to_enable_applications.diff'
        'monitor_gnome_a11y_schema_keys.diff')
sha1sums=('e465eb33b79394150514ad0af201bf32f07fd520'
          '916dc6519a7778e97cd8dd18c46363e6c36d389a'
          '0f8f3548b4cfda4eb8d5472d039b4f9744328e85'
          '2dd699e78173421338d498e2c0bacd13549b564b'
          '03ff9874630dec061590009b78d9cae3caf81398'
          '721590eba34b3c44b07f5ce4c715e3e7dd4df450')
install=${pkgbase}.install

prepare() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    # https://github.com/mate-desktop/mate-settings-daemon/commit/163f01a8b527c1ed801f68681fc7d82a54d42b1c
    patch -Np1 -i "${srcdir}/use_galculator_first.diff"
    # https://github.com/mate-desktop/mate-settings-daemon/compare/af56e3974762...4cb2701b4996
    patch -Np1 -i "${srcdir}/add_a11y_settings_plugin.diff"
    patch -Np1 -i "${srcdir}/move_a11y_keybindings_to_media-keys_plugin.diff"
    patch -Np1 -i "${srcdir}/use_gnome_a11y_schema_to_enable_applications.diff"
    patch -Np1 -i "${srcdir}/monitor_gnome_a11y_schema_keys.diff"
}

build() {
    cd "${srcdir}"
    cp -a ${pkgbase}-${pkgver}{,-gstreamer}

    # build for pulseaudio
    cd "${srcdir}/${pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgbase} \
        --sysconfdir=/etc \
        --with-gtk=2.0 \
        --enable-polkit \
        --enable-pulse \
        --disable-gstreamer \
        --disable-static
    make

    # build for gstreamer
    cd "${srcdir}/${pkgbase}-${pkgver}-gstreamer"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgbase} \
        --sysconfdir=/etc \
        --with-gtk=2.0 \
        --enable-polkit \
        --enable-gstreamer \
        --disable-pulse \
        --disable-static
    make
}

package_mate-settings-daemon-pulseaudio() {
    groups=('mate')
    pkgdesc="The MATE Settings daemon (pulseaudio)"
    depends=('dbus-glib' 'dconf' 'fontconfig' 'gtk2' 'libcanberra-pulse'
            'libmatekbd' 'libnotify' 'libxt' 'mate-desktop' 'nss' 'polkit'
            'pulseaudio-alsa')
    conflicts=("${pkgbase}-gstreamer" 'mate-media-gstreamer')
    provides=("${pkgbase}")
    replaces=("${pkgbase}")
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}

package_mate-settings-daemon-gstreamer() {
    pkgdesc="The MATE Settings daemon (GStreamer)"
    depends=('dbus-glib' 'dconf' 'fontconfig' 'gstreamer0.10-base' 'gtk2'
             'libmatekbd' 'libnotify' 'libxt' 'mate-desktop' 'nss' 'polkit')
    conflicts=("${pkgbase}-pulseaudio" 'mate-media-pulseaudio')
    provides=("${pkgbase}")
    cd "${srcdir}/${pkgbase}-${pkgver}-gstreamer"
    make DESTDIR="${pkgdir}" install
}
