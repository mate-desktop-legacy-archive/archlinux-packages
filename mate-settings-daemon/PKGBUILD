#Mantainer: Giovanni Ricciardi <kar98k.sniper@gmail.com>
#Contributor: Xpander <xpander0@gmail.com>

pkgname=mate-settings-daemon
pkgver=1.6.0
pkgrel=1
pkgdesc="The MATE Settings daemon"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libmatekbd' 'mate-desktop' 'libnotify' 'gstreamer0.10-base' 'hicolor-icon-theme')
makedepends=('intltool' 'pkgconfig')
options=('!libtool')
url="http://mate-desktop.org"
install=mate-settings-daemon.install
groups=('mate')
source=(http://pub.mate-desktop.org/releases/1.6/$pkgname-$pkgver.tar.xz)
sha256sums=('4203861f730ae12402803e805320ebb7301ad36be7cc631f3c00827669773005')


build() {
    cd "$srcdir/$pkgname-$pkgver"
    # Remove --disable-pulse if you want pulseaudio support
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/mate-settings-daemon \
        --disable-static \
        --disable-pulse \
        --disable-schemas-install \
        --enable-polkit || return 1
    make || return 1
}

package() {
    cd "${srcdir}/$pkgname-$pkgver"

    make DESTDIR="$pkgdir/" install
}
