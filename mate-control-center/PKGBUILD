# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.0
pkgbase=mate-control-center
pkgname=(${pkgbase})
pkgver=0
pkgrel=1
pkgdesc="The Control Center for MATE (GTK2 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('caja>=1.9.3' 'dbus-glib' 'dconf' 'desktop-file-utils' 'gsettings-desktop-schemas'
         'gtk2' 'mate-desktop>=1.9.3' 'mate-icon-theme' 'mate-menus>=1.9.1' 'mate-settings-daemon>=1.9.5'
         'marco>=1.9.3' 'libgtop' 'libmatekbd>=1.9.1' 'libunique' 'librsvg' 'libxss'
         'libxt' 'startup-notification')
makedepends=('git' 'docbook2x' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-unstable')
conflicts=("${pkgbase}-gtk3")
source=("${pkgbase}"::"git+https://github.com/mate-desktop/${pkgbase}.git")
sha1sums=('SKIP')
install=${pkgbase}.install

pkgver() {
    cd "${srcdir}/${pkgbase}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgbase}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --disable-static \
        --disable-update-mimedb
    make
}

package_mate-control-center() {
    cd "${srcdir}/${pkgbase}"
    make DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/share/applications/mimeinfo.cache"
}
