# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-control-center
pkgver=1.6.9.158.922d0e0
pkgrel=1
pkgdesc="The Control Center for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('caja' 'dbus-glib' 'dconf' 'desktop-file-utils' 'gsettings-desktop-schemas'
         'gtk2' 'mate-desktop' 'mate-icon-theme' 'mate-menus' 'mate-settings-daemon'
         'marco' 'libgtop' 'libmatekbd' 'libunique' 'librsvg' 'libxss'
         'libxt' 'startup-notification' 'yelp')
makedepends=('docbook2x' 'mate-common' 'perl-xml-parser' 'yelp-tools')
options=('!emptydirs' '!libtool')
groups=('mate')
source=("${pkgname}::git+https://github.com/mate-desktop/${pkgname}.git")
sha1sums=('SKIP')
install=${pkgname}.install

pkgver() {
    cd ${srcdir}/${pkgname}
    echo 1.6.9.$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
    cd ${srcdir}/${pkgname}
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --localstatedir=/var \
        --disable-static \
        --disable-update-mimedb
    make
}

package() {
    cd ${srcdir}/${pkgname}
    make DESTDIR=${pkgdir} install
    rm -f ${pkgdir}/usr/share/applications/mimeinfo.cache
}
