# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-session-manager
pkgver=1.7.1
pkgrel=2
pkgdesc="The MATE Session Handler"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('dbus-glib' 'gtk2' 'libsm' 'libxtst' 'mate-desktop' 'mate-polkit'
         'mate-settings-daemon' 'upower' 'xdg-user-dirs')
makedepends=('mate-common' 'perl-xml-parser' 'xmlto')
options=('!emptydirs')
groups=('mate-unstable')
source=("http://pub.mate-desktop.org/releases/1.7/${pkgname}-${pkgver}.tar.xz")
sha1sums=('9642cfc2d3fb6db1ae5f58842b9566fe5b7eb713')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=2.0
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
