# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Piotr Gorski <sir_lucjan@bridgelinux.pl>
# Contributor: Xpander <xpander0@gmail.com>

pkgname=mate-session-manager
pkgver=1.7.0
pkgrel=1
pkgdesc="The MATE Session Handler"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL' 'LGPL')
depends=('caja' 'dbus' 'desktop-file-utils' 'gobject-introspection' 'gtk2'
         'libice' 'libsm' 'libxau' 'libxt' 'libxtst' 'marco' 'mate-polkit'
         'mate-settings-daemon' 'pangox-compat' 'startup-notification' 'systemd'
         'upower')
makedepends=('mate-common' 'perl-xml-parser' 'xmlto')
options=('!emptydirs')
groups=('mate')
source=("http://pub.mate-desktop.org/releases/1.7/${pkgname}-${pkgver}.tar.xz")
sha1sums=('c86bca622c4dbcf3dac56db7da9f7511c79d33f5')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --localstatedir=/var
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
