# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=atril
pkgver=1.6.9.99.e5a6eba
pkgrel=1
pkgdesc="Simply a document viewer"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dconf' 'dbus' 'desktop-file-utils' 'gtk2' 'libsm' 'libspectre'
         'mate-icon-theme' 'poppler-glib' 'zlib')
makedepends=('caja' 'djvulibre' 'git' 'gobject-introspection' 'libgxps' 'mate-common'
             'perl-xml-parser' 'texlive-bin' 'yelp-tools')
optdepends=('caja: Document Properties extension'
            'djvulibre: DjVu support'
            'libgxps: XPS support'
            'p7zip: cbz and cb7 compressed comic books'
            'texlive-bin: DVI support'
            'unrar: cbr compressed comic books'
            'yelp: for reading MATE help documents')
replaces=('mate-document-viewer')
provides=('mate-document-viewer')
conflicts=('mate-document-viewer')
options=('!emptydirs')
groups=('mate-extra')
source=("${pkgname}::git+https://github.com/mate-desktop/${pkgname}.git")
sha1sums=('SKIP')
install=${pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo 1.6.9.$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
    cd "${srcdir}/${pkgname}"
    ./autogen.sh \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --with-gtk=2.0 \
        --enable-gtk-doc \
        --enable-djvu \
        --enable-dvi \
        --enable-t1lib \
        --enable-pixbuf \
        --enable-comics \
        --enable-introspection \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
