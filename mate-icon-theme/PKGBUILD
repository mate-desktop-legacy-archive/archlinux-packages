# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Piotr Gorski <sir_lucjan@bridgelinux.pl>
# Contributor: Xpander <xpander0@gmail.com>

pkgname=mate-icon-theme
pkgver=1.7.0
pkgrel=2
pkgdesc="MATE icon theme"
url="http://mate-desktop.org"
arch=('any')
license=('GPL')
depends=('hicolor-icon-theme' 'gtk-update-icon-cache' 'librsvg')
makedepends=('icon-naming-utils' 'mate-common' 'perl-xml-parser')
options=(!strip)
groups=('mate')
source=(http://pub.mate-desktop.org/releases/1.7/${pkgname}-${pkgver}.tar.xz)
sha1sums=('8ffc0026aa5570a1ddf31d214fa583ce0c708bdc')
install=${pkgname}.install

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
    rm -f ${pkgdir}/usr/share/icons/mate/icon-theme.cache
}
