# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-icon-theme
pkgver=1.7.90
pkgrel=1
pkgdesc="MATE icon theme"
url="http://mate-desktop.org"
arch=('any')
license=('GPL')
depends=('hicolor-icon-theme' 'gtk-update-icon-cache')
makedepends=('icon-naming-utils' 'mate-common' 'perl-xml-parser')
options=(!emptydirs !strip)
groups=('mate-unstable')
source=("http://pub.mate-desktop.org/releases/1.7/${pkgname}-${pkgver}.tar.xz")
sha1sums=('e6b00cbf8e47bcab50d8cd3b12fb90528578e73c')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/share/icons/mate/icon-theme.cache"
}
