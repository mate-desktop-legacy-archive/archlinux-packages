# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=libmateweather
pkgver=1.8.0
pkgrel=1
pkgdesc="Provides access to weather information from the Internet."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('dconf' 'gtk2' 'libsoup' 'pygtk' 'python2-gobject2')
makedepends=('mate-common' 'perl-xml-parser')
options=('!emptydirs')
source=("http://pub.mate-desktop.org/releases/1.8/${pkgname}-${pkgver}.tar.xz")
sha1sums=('80ca08ff1b8d55313a32f9fe4ffbc4907609b6cd')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --disable-static \
        --enable-python \
        --enable-locations-compression
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
