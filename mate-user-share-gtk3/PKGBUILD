#Maintainer: Martin Wimpress <code@flexion.org>

_ver=1.9.0
pkgbase=mate-user-share
pkgname=(${pkgbase}-gtk3)
pkgver=0
pkgrel=1
pkgdesc="User level public file sharing via WebDAV for MATE."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'dconf' 'libunique' 'mod_dnssd')
makedepends=('git' 'caja-gtk3' 'libcanberra' 'libnotify' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-unstable-extra-gtk3')
provides=("${pkgbase}")
replaces=("${pkgbase}")
conflicts=("${pkgbase}")
source=("${pkgbase}"::"git+https://github.com/mate-desktop/${pkgbase}.git")
sha1sums=('SKIP')
install=${pkgbase}.install

pkgver() {
    cd "${srcdir}/${pkgbase}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgbase}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --libexec=/usr/lib/${pkgbase} \
        --sysconfdir=/etc \
        --disable-static \
        --disable-bluetooth
    make
}

package_mate-user-share-gtk3() {
    cd "${srcdir}/${pkgbase}"
    make  DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/share/mate-user-share/dav_user_2.0.conf"
}
