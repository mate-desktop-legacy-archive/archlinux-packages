# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-system-tools
pkgver=1.7.0
pkgrel=1
pkgdesc="Suite of tools for MATE to simplify configuring workstations."
url="http://mate-desktop.org/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dconf' 'liboobs' 'mate-polkit' 'system-tools-backends' 'wireless_tools')
makedepends=('caja' 'mate-common' 'perl-xml-parser' 'yelp-tools')
optdepends=('ntp: Automatic time synchronisation.'
            'caja: provides Caja Shares extension'
            'yelp: for reading MATE help documents')
source=("http://pub.mate-desktop.org/releases/1.7/${pkgname}-${pkgver}.tar.xz")
sha1sums=('fe812716ac67625d069a7a1f1e727e682c95f452')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --with-gtk=2.0
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/lib/caja/extensions-2.0/libcaja-gst-shares.a"
}
