# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-system-tools
pkgver=1.8.0
pkgrel=2
pkgdesc="Suite of tools for MATE to simplify configuring workstations."
url="http://mate-desktop.org/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dconf' 'liboobs' 'mate-polkit' 'system-tools-backends')
makedepends=('caja' 'mate-common' 'perl-xml-parser' 'yelp-tools')
optdepends=('caja: provides Caja Shares extension'
            'yelp: for reading MATE help documents')
source=("http://pub.mate-desktop.org/releases/1.8/${pkgname}-${pkgver}.tar.xz")
sha1sums=('11a8d72cf265834867352ad67a2d32485dbc98e0')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --disable-network \
        --disable-services \
        --disable-time
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
