# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-system-tools
pkgver=1.6.9.50.95ad0ad
pkgrel=1
pkgdesc="Suite of tools for MATE to simplify configuring workstations."
url="http://mate-desktop.org/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dconf' 'gtk2' 'liboobs' 'mate-polkit' 'system-tools-backends'
         'wireless_tools')
makedepends=('caja' 'git' 'mate-common' 'perl-xml-parser' 'yelp-tools')
optdepends=('ntp: Automatic time synchronisation.'
            'mate-file-manager: provides Caja Shares extension'
            'yelp: for reading MATE help documents')
groups=('mate-extra')
source=("${pkgname}::git+https://github.com/mate-desktop/${pkgname}.git")
sha1sums=('SKIP')
install=${pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo 1.6.9.$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
    cd "${srcdir}/${pkgname}"
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --with-gtk=2.0
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/lib/caja/extensions-2.0/libcaja-gst-shares.a"
}
