# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-system-tools
pkgver=1.6.9.48.8349af2
pkgrel=1
pkgdesc="Suite of tools for MATE to simplify configuring workstations."
url="http://mate-desktop.org/"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('caja' 'desktop-file-utils' 'gtk2' 'liboobs' 'mate-polkit'
         'system-tools-backends' 'yelp')
makedepends=('mate-common' 'perl-xml-parser' 'yelp-tools')
optdepends=('ntp: Automatic time syncronisation.'
            'system-config-printer: Add CUPS printer configuration tool.'
            'cups-pk-helper: A helper that makes system-config-printer use PolicyKit.')
groups=('mate-extras')
source=("${pkgname}::git+https://github.com/mate-desktop/${pkgname}.git")
sha1sums=('SKIP')
install=${pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo 1.6.9.$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
    cd "${srcdir}/${pkgname}"
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/lib/caja/extensions-2.0/libcaja-gst-shares.a"
}
