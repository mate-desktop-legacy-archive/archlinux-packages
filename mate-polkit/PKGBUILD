# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-polkit
pkgver=1.7.90
pkgrel=1
pkgdesc="PolicyKit integration for the MATE desktop"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gtk2' 'polkit')
makedepends=('gobject-introspection' 'mate-common' 'perl-xml-parser')
options=('!emptydirs')
groups=('mate-unstable')
source=("http://pub.mate-desktop.org/releases/1.7/${pkgname}-${pkgver}.tar.xz")
sha1sums=('f1f96095b040385826122d4dac6ed38e5adb77be')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --enable-introspection \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
